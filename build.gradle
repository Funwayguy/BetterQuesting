plugins {
    id "maven-publish"
    id "idea"
    id "net.minecraftforge.gradle" version "5.+"
}

minecraft {
    mappings channel: "stable", version: "39-1.12"

    runs {
        server {
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
        }

        client {
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
        }
    }
}

group "betterquesting"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraftVersion}-${forgeVersion}"
}

processResources {
    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraftVersion
    }
}

jar {
    finalizedBy "reobfJar"
}

sourceSets.main {
    output.resourcesDir = output.classesDirs.singleFile
}

tasks.register('deobfJar', Jar) {
    from sourceSets.main.output
    archiveClassifier = "deobf"
}

tasks.register('apiJar', Jar) {
    from(sourceSets.main.output) {
        include 'betterquesting/api/**'
        include 'betterquesting/api2/**'
    }
    archiveClassifier = "api"
}

artifacts {
    archives deobfJar
    archives apiJar
}
